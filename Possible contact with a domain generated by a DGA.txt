
// Title: Possible contact with a domain generated by a DGA
// Description: Detects potential DGA domains using trigram analysis and connects them back to actual network activity.
// MITRE ATT&CK: T1568.002
// Tactics: TA0011 - Command and Control
// Data Sources: Network Connection, DNS Query

let triThreshold = 500;
let startTime = 6h;
let dgaLengthThreshold = 8;
let top1M = externaldata(Position:int, Domain:string)
    [@"http://s3-us-west-1.amazonaws.com/umbrella-static/top-1m.csv.zip"]
    with (format="csv", zipPattern="*.csv");
let triBaseline = top1M
| extend Domain = tolower(extract("([^.]*).{0,7}$", 1, Domain))
| extend AllTriGrams = array_concat(
    extract_all("(...)", Domain),
    extract_all("(...)", substring(Domain, 1)),
    extract_all("(...)", substring(Domain, 2))
)
| mvexpand Trigram = AllTriGrams
| summarize triCount = count() by tostring(Trigram)
| where triCount > triThreshold
| distinct Trigram;
let allDataSummarized = CommonSecurityLog
| where TimeGenerated > ago(startTime)
| where isnotempty(DestinationHostName)
| extend Name = tolower(DestinationHostName)
| distinct Name
| where Name has "." and not(Name endswith ".home") and not(Name endswith ".lan")
| extend DGADomain = extract("([^.]*).{0,7}$", 1, Name)
| where strlen(DGADomain) > dgaLengthThreshold
| where DGADomain matches regex "^[A-Za-z]+$"
| extend AllTriGrams = array_concat(
    extract_all("(...)", DGADomain),
    extract_all("(...)", substring(DGADomain, 1)),
    extract_all("(...)", substring(DGADomain, 2))
);
let nonRepeatingTris = allDataSummarized
| join kind=leftanti (
    allDataSummarized
    | mvexpand AllTriGrams
    | summarize count() by tostring(AllTriGrams), DGADomain
    | where count_ > 1
    | distinct DGADomain
) on DGADomain;
let dataWithRareTris = nonRepeatingTris
| join kind=leftanti (
    nonRepeatingTris
    | mvexpand AllTriGrams
    | extend Trigram = tostring(AllTriGrams)
    | distinct Trigram, DGADomain
    | join kind=inner (triBaseline) on Trigram
    | distinct DGADomain
) on DGADomain;
dataWithRareTris
| join kind=inner (
    CommonSecurityLog
    | where TimeGenerated > ago(startTime)
    | where isnotempty(DestinationHostName)
    | extend DestinationHostName = tolower(DestinationHostName)
    | project-rename Name=DestinationHostName, DataSource=DeviceVendor, DeviceAction
    | where DeviceAction in~ ("connect", "ip-conn", "dns", "passthrough", "login", "negotiate", "dpd")
    | summarize StartTime = min(TimeGenerated), EndTime = max(TimeGenerated) by Name, SourceIP, DestinationIP, DataSource, DeviceAction
) on Name
| project StartTime, EndTime, Name, DGADomain, SourceIP, DestinationIP, DataSource, DeviceAction
